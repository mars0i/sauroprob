;; Exploration of example on p. 192 of Seger and Brockmann,
;; "What is bet-hedging?",
;; pp. 182--211 in <em>Oxford Surveys in Evolutionary Biology</em> v. 4,
;; 1987, eds. Paul H. Harvey and Linda Partridge.

^:kindly/hide-code
(ns segerbrockmann
  (:require [clojure.math :as m]
            [scicloj.kindly.v4.kind :as kind]
            [scicloj.tableplot.v1.plotly :as plotly]
            [fastmath.random :as fr] ; see note below
            ;[fastmath.stats :as fs]
            [tablecloth.api :as tc]
            ;[clojisr.v1.r :as R]
            ;[criterium.core :as crit]
            [utils.math :as um]
            ;[utils.misc :as msc]
            [sauroprob.plotly :as sp]
            [sauroprob.iterfreqs-fns :as fns]))

;; Not really about chaos
;; From Seger and Brockman 1987 p. 192 on bet hedging
;; Maybe should go in a different repo.

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Random numbers

;; NOTE Using fastmath.random to generate random numbers, even though
;; at present (9/2025) it doesn't include my preferred PRNGs.  I should
;; probably submit a PR to fastmath to include PRNGs that I want.

;; Another version is in random_utils.clj
(defn make-seed
  "Return a long generated by fastmath's default prng after resetting its seed."
  []
  (fr/set-seed)
  (fr/lrand))

(defn flush-rng
  "Draws n numbers from rng to flush out possible initial nonrandomness, e.g.
  to make it likely that we're beyond zeroland."
  [rng n]
  (dotimes [i n]
    (fr/drandom rng)))

(def seed (make-seed))
(def rng (fr/rng :well1024a seed))
(flush-rng rng 10000) ; maybe not needed given seed generation

(comment
  (make-seed)
  (fr/set-seed)
  (fr/lrand)
  (flush-rng rng 10000)
  (fr/drandom rng)
  (take 12 (repeatedly (fn [] (fr/brandom rng 0.8))))
  (fr/randval)
)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Probabilities of environments 

(def wet-env-relf 1/2)
(def dry-env-relf (- 1 wet-env-relf))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Maps and functions for particular fitness strategies

;; Simple strategies can be defined by maps.
(def dry-specialist-fits "A_2 in S&B p. 192" {:wet-env 0.6, :dry-env 1})
(def wet-specialist-fits "A_1 in S&B p. 192" {:wet-env 1, :dry-env 0.58})
(def generalist-fits "A_3, jack-of-all-trades in S&B p. 192" {:wet-env 0.785 :dry-env 0.785})

;; Probabilistic polymorphic phenotype A_4:

(def poly-fits-dry-spec-prob 0.56)  ; prob of emulating the dry specialist phenotype
(def poly-fits-wet-spec-prob (- 1 poly-fits-dry-spec-prob)) ; prob of emulating wet specialist

;; Seger & Brockmann just assume that the probabilistic polymorphic 
;; genotype subpop is large, so that the fitness of the genotype 
;; in each generation in each environment is its arithmetic mean fitness.
;; So for their modeling, we can define a map, just as for the specialists
;; and generalist:

(def poly-avg-fits "A_4, probabilistic polymorphic, S&B p. 192"
  {:wet-env (+ (* poly-fits-dry-spec-prob (dry-specialist-fits :wet-env))  ; 0.776
               (* poly-fits-wet-spec-prob (wet-specialist-fits :wet-env)))
   :dry-env (+ (* poly-fits-dry-spec-prob (dry-specialist-fits :dry-env))  ; 0.8152
               (* poly-fits-wet-spec-prob (wet-specialist-fits :dry-env)))})

;; However, a truly probabilistic strategy that exhibits stochastic
;; behaviore has to be defined by an actual function:
(defn poly-fits
  "A_4, 'the phenotypically polymorphic allele', S&B p. 192.  env should be
  :dry-env or :wet-env.  Returns a fitness value for the organism in that
  environment.  The dry-specialist or wet-specialist phenotypes are chosen
  randomly with probability dry-specialist prob 0.56 for the dry-specialist 
  phenotype, and 1 minus that avalue for the wet-specialist phenotype."
  [rng env]
  (if (fr/brandom rng poly-fits-dry-spec-prob)
    (dry-specialist-fits env)
    (wet-specialist-fits env)))

(comment
  (poly-fits rng :wet-env)
  (take 15 (repeatedly (fn [] (poly-fits rng :wet-env))))
  (poly-fits rng :dry-env)
  (take 15 (repeatedly (fn [] (poly-fits rng :dry-env))))
)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Functions for calculating summary statistics

(defn pop-summary
  "t1-fits is a map of fitnesses for trait t1, with keys :wet-env and
  :dry-env representing fitnesses in the wet and dry envs, respectively.
  Mutatis mutandis for t2-fits. Alternatively, t<n>-fits could be a
  function expects :wet-env or :dry-env as argument, and returns a fitness
  value (perhaps somewhat randomly). summary-fn is a function such as
  arithmetic mean or geometric mean.  t2-relf is the relative frequency of
  trait t2 in a population with two traits present (so the relative
  frequency of t1 is (1 - relf of t2). [e.g. if you're plotting the
  frequency of an invader from left to right, it starts with relf near
  zero. So the invader's fitnesse should be in t2-fits.]"
  [summary-fn t1-fits t2-fits t2-relf]
  (let [t1-relf (- 1 t2-relf)
        ;; pop means:
        t1-in-wet (* t1-relf (t1-fits :wet-env))
        t1-in-dry (* t1-relf (t1-fits :dry-env))
        t2-in-wet (* t2-relf (t2-fits :wet-env))
        t2-in-dry (* t2-relf (t2-fits :dry-env))
        wet-env-sum (+ t1-in-wet t2-in-wet)
        dry-env-sum (+ t2-in-dry t1-in-dry)]
    (summary-fn dry-env-relf dry-env-sum wet-env-sum)))

(defn pop-arith-mean
  [t1-env-relf t1-env-sum t2-env-sum]
  (let [t2-env-relf (- 1 t1-env-relf)]
    (+ (* t1-env-sum t1-env-relf)
       (* t2-env-sum t2-env-relf))))

(defn pop-geom-mean
  [t1-env-relf t1-env-sum t2-env-sum]
  (let [t2-env-relf (- 1 t1-env-relf)]
    (* (m/pow t1-env-sum t1-env-relf)
       (m/pow t2-env-sum t2-env-relf))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Plots

;; pop arith mean: wet specialist vs invading dry specialist:
(let [step 0.01
      xs (range 0 (+ 1 step) step)
      ys (map (partial pop-summary pop-arith-mean wet-specialist-fits dry-specialist-fits)
              xs)]
  (-> (tc/dataset {:x xs :y ys})
      (plotly/layer-line {:=x :x, :=y, :y})
      plotly/plot))

;; pop geom mean: wet specialist vs invading dry specialist:
(let [step 0.01
      xs (range 0 (+ 1 step) step)
      ys (map (partial pop-summary pop-geom-mean wet-specialist-fits dry-specialist-fits)
              xs)]
  (-> (tc/dataset {:x xs :y ys :x-vert [0.56 0.56] :y-vert [0.76 0.796]}) ; add vert line at max
      (plotly/layer-line {:=x :x, :=y, :y})
      (plotly/layer-line {:=x :x-vert, :=y, :y-vert})
      plotly/plot
      (sp/set-line-width 1 1.0)  ; y=x
      (sp/set-line-dash 1 "dot")
      ;(assoc-in [:data 0 :line :width] 1)
      ))

;; pop arith mean: dry specialist vs invading generalist:    
;; Q: Why is this curve going down?    
;; A: Because generalist's arith mean fitness less than dry specalist's.    
;; Nevertheless the generalist invades since its geom mean fitness is greater.
(let [step 0.01
      xs (range 0 (+ 1 step) step)
      ys (map (partial pop-summary pop-arith-mean dry-specialist-fits generalist-fits)
              xs)]
  (-> (tc/dataset {:x xs :y ys})
      (plotly/layer-line {:=x :x, :=y, :y})
      plotly/plot
      ;(assoc-in [:data 0 :line :width] 1)
      ))

;; pop geom mean: dry specialist vs invading generalist:
(let [step 0.01
      xs (range 0 (+ 1 step) step)
      ys (map (partial pop-summary pop-geom-mean dry-specialist-fits generalist-fits)
              xs)]
  (-> (tc/dataset {:x xs :y ys 
                   ;:x-vert [0.56 0.56] :y-vert [0.76 0.796]
                  })
      (plotly/layer-line {:=x :x, :=y, :y})
      ;(plotly/layer-line {:=x :x-vert, :=y, :y-vert})
      plotly/plot
      ;(assoc-in [:data 0 :line :width] 1)
      ))


;; pop arith mean: generalist vs invading probabilistic polymorphic
(let [step 0.01
      xs (range 0 (+ 1 step) step)
      ys (map (partial pop-summary pop-arith-mean generalist-fits poly-avg-fits)
              xs)]
  (-> (tc/dataset {:x xs :y ys})
      (plotly/layer-line {:=x :x, :=y, :y})
      plotly/plot
      ;(assoc-in [:data 0 :line :width] 1)
      ))

;; pop geom mean: generalist vs invading probabilistic polymorphic    
;; Q: Is this right? Does the pop geom mean simply increase with invasion?
(let [step 0.01
      xs (range 0 (+ 1 step) step)
      ys (map (partial pop-summary pop-geom-mean generalist-fits poly-avg-fits)
              xs)]
  (-> (tc/dataset {:x xs :y ys 
                   ;:x-vert [0.56 0.56] :y-vert [0.76 0.796]
                  })
      (plotly/layer-line {:=x :x, :=y, :y})
      ;(plotly/layer-line {:=x :x-vert, :=y, :y-vert})
      plotly/plot
      ;(assoc-in [:data 0 :line :width] 1)
      ))
nil
